apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'org.greenrobot.greendao'


Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
def _storeFile = properties.getProperty('storeFile')
def _storePassword = properties.getProperty('storePassword')
def _keyAlias = properties.getProperty('keyAlias')
def _keyPassword = properties.getProperty('keyPassword')
def httpDnsId = properties.getProperty('httpDnsId')
def httpDnsSecret = properties.getProperty('httpDnsSecret')

android {
    compileSdkVersion 29
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        applicationId 'me.ykrank.s1next'
        minSdkVersion 19
        targetSdkVersion 27
        versionCode 69
        versionName '2.4.3.1'
        vectorDrawables.useSupportLibrary = true
    }
    signingConfigs {
        if (_storeFile != null && !_storeFile.isEmpty()) {
            release {
                storeFile file(_storeFile)
                storePassword _storePassword
                keyAlias _keyAlias
                keyPassword _keyPassword
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    dataBinding {
        enabled = true
    }
    dexOptions {
        preDexLibraries = Boolean.valueOf(System.getProperty('pre-dex-libraries', 'true'))
    }
    sourceSets {
        main.java.srcDirs += 'build/generated/source/greendao'
        main.java.srcDirs += 'src/main/kotlin'
    }
    lintOptions {
        abortOnError = Boolean.valueOf(System.getProperty('abort-on-error', 'true'))
        textReport true
        textOutput 'stdout'
        disable 'MissingTranslation'
    }
    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
    }
    buildTypes {
        debug {
            multiDexEnabled true
            applicationIdSuffix '.debug'
            versionNameSuffix '-debug'
            signingConfig signingConfigs.release
        }

        alpha {
            multiDexEnabled true
            applicationIdSuffix '.alpha'
            versionNameSuffix '-alpha'
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            matchingFallbacks = ['release', 'debug']
        }

        release {
            multiDexEnabled true
            if (signingConfigs.hasProperty('release')) {
                signingConfig signingConfigs.release
            }
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    buildTypes.each {
        it.buildConfigField "String", "DB_NAME", '"s1.db"'
        it.buildConfigField "String", "HTTP_DNS_ID", httpDnsId
        it.buildConfigField "String", "HTTP_DNS_SECRET", httpDnsSecret
    }
    flavorDimensions "market"
    productFlavors {
        play {
            dimension "market"
            manifestPlaceholders["APP_CHANNEL"] = "play.google.com"
            versionNameSuffix '-play'
        }
        normal {
            dimension "market"
            manifestPlaceholders["APP_CHANNEL"] = "normal"
        }
    }
}

kapt {
    mapDiagnosticLocations = true
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
//    options.compilerArgs << '-Xlint:unchecked,deprecation'
    options.compilerArgs << "-Xmaxerrs" << "500"
}

greendao {
    schemaVersion 7
}

//http://stackoverflow.com/questions/28683327/how-to-check-if-gradle-dependency-has-new-version
dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    playImplementation fileTree(include: ['*.jar'], dir: 'libs/flavors/play')
    normalImplementation fileTree(include: ['*.jar'], dir: 'libs/flavors/normal')

    normalImplementation "com.github.ykrank.AndroidTools:library:1.4.2.4:normal@aar"
    playImplementation "com.github.ykrank.AndroidTools:library:1.4.2.4:play@aar"
    implementation 'com.github.ykrank.AndroidTools:JKeyboardPanelSwitch:v1.2.0'

    // Library start
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    //For jackson
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    implementation "com.google.android.material:material:1.3.0-alpha02"
    implementation "androidx.cardview:cardview:1.0.0"
    implementation "androidx.percentlayout:percentlayout:1.0.0"
    implementation "androidx.legacy:legacy-preference-v14:1.0.0"
    implementation "androidx.legacy:legacy-support-v4:1.0.0"
    implementation 'androidx.constraintlayout:constraintlayout:2.0.1'

    implementation 'androidx.multidex:multidex:2.0.1'

    implementation 'org.apache.commons:commons-lang3:3.11'
    implementation 'com.google.guava:guava:29.0-android'

    implementation 'io.reactivex.rxjava2:rxjava:2.2.19'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'io.reactivex.rxjava2:rxkotlin:2.4.0'

    implementation 'com.github.bumptech.glide:glide:4.11.0'
    implementation 'com.github.bumptech.glide:okhttp3-integration:4.11.0'
    //okhttp 3.12.0 have bug https://github.com/square/okhttp/issues/4395
    implementation 'com.squareup.okhttp3:okhttp:4.9.0'

    implementation 'com.shizhefei:LargeImageView:1.1.0'
    implementation 'com.github.ykrank:androidautodispose:0.4.0'
    implementation 'com.hannesdorfmann:adapterdelegates3:3.1.0'

    playImplementation 'com.tencent.bugly:crashreport:3.2.3'
    normalImplementation 'com.tencent.bugly:crashreport_upgrade:1.4.2'
    implementation 'com.tencent.bugly:nativecrashreport:3.7.47'

    implementation 'com.orhanobut:logger:2.2.0'

    //leakcanary
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.4'
    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.6.3'
    alphaImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.6.3'
    // Library end

    //FilePicker
    implementation 'com.nononsenseapps:filepicker:4.1.0'

//    compileOnly 'com.google.code.findbugs:jsr305:3.0.2'
//    compileOnly 'com.google.j2objc:j2objc-annotations:1.3'

    implementation 'com.google.dagger:dagger:2.28.3'
    kapt 'com.google.dagger:dagger-compiler:2.28.3'
    // https://github.com/google/dagger/issues/95
//    provided 'javax.annotation:javax.annotation-api:1.3'

    implementation 'androidx.transition:transition:1.3.1'

    implementation 'com.squareup.okhttp3:okhttp-urlconnection:4.8.0'
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.9.0'
    implementation 'com.squareup.retrofit2:converter-jackson:2.9.0'
    implementation 'com.squareup.retrofit2:converter-scalars:2.9.0'

    implementation "com.github.VictorAlbertos.RxCache:runtime:1.8.3-2.x"
    implementation 'com.github.VictorAlbertos.Jolyglot:jackson:0.0.4'

    implementation 'com.fasterxml.jackson.core:jackson-databind:2.11.0'
    implementation "com.fasterxml.jackson.module:jackson-module-kotlin:2.11.0"

    implementation 'nz.bradcampbell:paperparcel:2.0.8'
    implementation 'nz.bradcampbell:paperparcel-kotlin:2.0.8' // Optional
    implementation "nz.bradcampbell:paperparcel-api:2.0.8"
    kapt 'nz.bradcampbell:paperparcel-compiler:2.0.8'

    kapt 'com.github.bumptech.glide:compiler:4.11.0'

    implementation 'com.github.chrisbanes:PhotoView:2.1.4'
    implementation 'org.greenrobot:greendao:3.3.0'
//    debugImplementation 'net.zetetic:android-database-sqlcipher:4.4.0'
    implementation 'org.jsoup:jsoup:1.13.1'
    implementation 'com.bigkoo:quicksidebar:1.0.3'

//  flipper
    releaseImplementation 'com.facebook.flipper:flipper-noop:0.51.0'
    alphaImplementation 'com.facebook.flipper:flipper-noop:0.51.0'
    debugImplementation 'com.facebook.flipper:flipper:0.51.0'
    debugImplementation 'com.facebook.soloader:soloader:0.9.0'
    debugImplementation 'com.facebook.flipper:flipper-network-plugin:0.51.0'

    implementation 'com.nononsenseapps:filepicker:4.1.0'
    implementation 'com.liulishuo.okdownload:okdownload:1.0.7'
    implementation 'com.liulishuo.okdownload:okhttp:1.0.7'
    implementation 'com.liulishuo.okdownload:sqlite:1.0.7'

    implementation 'com.github.LuckSiege.PictureSelector:picture_library:v2.5.9'

    implementation('com.aliyun.ams:alicloud-android-httpdns:1.2.5@aar') {
        transitive true
    }

}
